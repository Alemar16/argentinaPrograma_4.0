//Ejercicio 9


//Los empleados de una fábrica trabajan en dos turnos: Diurno y Nocturno. Se desea calcular el 
//jornal diario de acuerdo con las siguientes reglas:

//	a) La tarifa de las horas diurnas es de $ 90
//	b) La tarifa de las horas nocturnas es de $ 125
//	c) En caso de ser feriado, la tarifa se incrementa en un 10% si el turno es diurno y en 
//		un 15% si el turno es nocturno.

//El programa debe solicitar la siguiente información al usuario: el nombre del trabajador, el día 
//de la semana, el turno (diurno o nocturno) y la cantidad de horas trabajadas. Además,
//debemos preguntarle al usuario si el día de la semana (lunes, martes, miércoles, etc.) era 
//festivo o no, para poder calcular el jornal diario. Utilice una función para realizar el cálculo.



Algoritmo jornadaTrabajo
	Definir nombre, dia, festivo, turno como cadena
	Definir horas Como Real
	
	Escribir "Ingrese el nombre del trabajador:"
	Leer nombre
	Escribir "Ingrese el día de la semana:" 
	Leer dia
	Escribir "¿El día ingresado fue festivo? (si/no)"
	Leer festivo
	Escribir "Ingrese el turno trabajado (diurno/nocturno)"
	Leer turno
	Escribir "Ingrese la cantidad de horas trabajadas:"
	Leer horas
	
	Escribir "El jornal para " nombre " es $" calcular(festivo,turno,horas)
	
FinAlgoritmo

Funcion jornal <- calcular(festivo,turno,horas)
	Definir jornal, tarifa Como Real
	
	Si festivo = "si"
		Si turno = "diurno"
			tarifa = 110*90/100
		SiNo // tarifa = nocturno
			tarifa = 115*125/100
		FinSi
	SiNo // festivo = no
		Si turno = "diurno"
			tarifa = 90
		SiNo
			tarifa = 125
		FinSi
	FinSi
	
	jornal = horas * tarifa
FinFuncion
